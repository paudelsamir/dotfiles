import qs
import qs.services
import qs.modules.common
import qs.modules.common.widgets
import qs.services
import "./todo/"
import "./monthlyGoals/"
import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import Qt5Compat.GraphicalEffects

Item {
    id: root
    required property var scopeRoot
    anchors.fill: parent
    property var tabButtonList: [
        {"icon": "task_alt", "name": Translation.tr("Todo")},
        {"icon": "flag", "name": Translation.tr("Monthly Goals")}
    ]
    property int selectedTab: 0

    function focusActiveItem() {
        swipeView.currentItem.forceActiveFocus()
    }

    Keys.onPressed: (event) => {
        if (event.modifiers === Qt.ControlModifier) {
            if (event.key === Qt.Key_PageDown) {
                root.selectedTab = Math.min(root.selectedTab + 1, root.tabButtonList.length - 1)
                event.accepted = true;
            } 
            else if (event.key === Qt.Key_PageUp) {
                root.selectedTab = Math.max(root.selectedTab - 1, 0)
                event.accepted = true;
            }
            else if (event.key === Qt.Key_Tab) {
                root.selectedTab = (root.selectedTab + 1) % root.tabButtonList.length;
                event.accepted = true;
            }
            else if (event.key === Qt.Key_Backtab) {
                root.selectedTab = (root.selectedTab - 1 + root.tabButtonList.length) % root.tabButtonList.length;
                event.accepted = true;
            }
        }
    }

    ColumnLayout {
        anchors.fill: parent
        anchors.margins: sidebarPadding
        spacing: sidebarPadding

        // Top Section - Todo/Monthly Goals (60% of sidebar)
        Rectangle {
            Layout.fillWidth: true
            Layout.fillHeight: true
            Layout.preferredHeight: parent.height * 0.6
            color: Appearance.colors.colLayer1
            radius: Appearance.rounding.normal
            clip: true

            ColumnLayout {
                anchors.fill: parent
                anchors.margins: 10
                spacing: 5

                PrimaryTabBar { // Tab strip
                    id: tabBar
                    Layout.fillHeight: false
                    tabButtonList: root.tabButtonList
                    externalTrackedTab: root.selectedTab
                    function onCurrentIndexChanged(currentIndex) {
                        root.selectedTab = currentIndex
                    }
                }

                SwipeView { // Content pages
                    id: swipeView
                    Layout.topMargin: 5
                    Layout.fillWidth: true
                    Layout.fillHeight: true
                    spacing: 10
                    
                    currentIndex: tabBar.externalTrackedTab
                    onCurrentIndexChanged: {
                        tabBar.enableIndicatorAnimation = true
                        root.selectedTab = currentIndex
                    }

                    clip: true
                    layer.enabled: true
                    layer.effect: OpacityMask {
                        maskSource: Rectangle {
                            width: swipeView.width
                            height: swipeView.height
                            radius: Appearance.rounding.small
                        }
                    }

                    contentChildren: [
                        todo.createObject(),
                        monthlyGoalsWithCalendar.createObject()
                    ]
                }
            }
        }

        // Bottom Section - Timer Tools (40% of sidebar)
        BottomWidgetGroup {
            Layout.alignment: Qt.AlignHCenter
            Layout.fillHeight: true
            Layout.fillWidth: true
            Layout.preferredHeight: parent.height * 0.4
        }

        Component {
            id: todo
            TodoWidget {}
        }
        Component {
            id: monthlyGoalsWithCalendar
            MonthlyGoalsWithCalendar {}
        }
        
    }
}